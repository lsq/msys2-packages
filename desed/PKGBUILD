# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=desed
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.2.dead5
pkgrel=1
pkgdesc="Debugger for Sed: demystify and debug your sed scripts, from comfort of your terminal."
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/SoptikHa2/desed'
license=('spdx:GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "git")
# optdepends=("${MINGW_PACKAGE_PREFIX}-fzf: for tui usage"
            # "${MINGW_PACKAGE_PREFIX}-delta: for diff colourizer")
# source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
source=("$_realname::git+https://github.com/soptikha2/desed.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_realname}"

    latest_version_published=$(git tag | tail -1 | sed 's/^v//')
    latest_commit_hash=$(git log -1 --format="%h")
    echo "$latest_version_published.$latest_commit_hash"
}
prepare() {
  cd "${srcdir}/${_realname}"

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${srcdir}/${_realname}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

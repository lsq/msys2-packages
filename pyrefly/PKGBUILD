# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Christoph Reiter <reiter.christoph@gmail.com>

_realname=pyrefly
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.42.1
pkgrel=1
pkgdesc="A fast type checker and IDE for Python (mingw-w64)"
arch=('any')
# mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
mingw_arch=('ucrt64''clangarm64')
url='https://pyrefly.org'
msys2_repository_url='https://github.com/facebook/pyrefly'
msys2_changelog_url='https://github.com/facebook/pyrefly/README.md'
license=('spdx:MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-rustup"
  "${MINGW_PACKAGE_PREFIX}-maturin"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  'git'
)
options=('!strip')
source=("${_realname}-${pkgver}.tar.gz::${msys2_repository_url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('709855106B5828F38764D3C59B02D7AA4F626216C1D82396769C839E0BF6932E')

prepare() {
  cd "${_realname}-${pkgver}/pyrefly"
  export RUSTUP_TOOLCHAIN=nightly-x86_64-pc-windows-gnu
  rustup set default-host x86_64-pc-windows-gnu
  rustup show
  rustup default nightly-x86_64-pc-windows-gnu
  rustup show

  cargo fetch --locked --config='net.git-fetch-with-cli=true' --target x86_64-pc-windows-gnu
}

build() {
  cd "${_realname}-${pkgver}/pyrefly"

  export _PYTHON_HOST_PLATFORM=$(python -c "import sysconfig, sys; sys.stdout.write(sysconfig.get_platform())")
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  rustup show
  maturin build --release --strip --frozen --all-features
}

check() {
  cd "${_realname}-${pkgver}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  cargo test --release --frozen --all-features
}

package_pyrefly() {
  install -Dm755 "${_realname}-${pkgver}/pyrefly/target/release/pyrefly" "${pkgdir}${MINGW_PREFIX}/bin/pyrefly"
  install -Dm644 "${_realname}-${pkgver}/pyrefly/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  # local _complete="${_realname}-${pkgver}/target/release/pyrefly generate-shell-completion"
  # $_complete bash | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/${_realname}"
  # $_complete fish | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/${_realname}.fish"
  # $_complete zsh | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_${_realname}"
}

package_python-pyrefly() {
  pkgdesc+=" (Python module)"
  depends=("${MINGW_PACKAGE_PREFIX}-python" "${MINGW_PACKAGE_PREFIX}-pyrefly")

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" "${_realname}-${pkgver}"/pyrefly/target/wheels/*.whl

  # split pyrefly binary
  rm -rf "${pkgdir}${MINGW_PREFIX}/bin/"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Christoph Reiter <reiter.christoph@gmail.com>

# template end;

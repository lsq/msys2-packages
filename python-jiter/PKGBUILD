# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

_realname=jiter
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.11.1
pkgrel=1
pkgdesc="Fast iterable JSON parser"
# mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
mingw_arch=('ucrt64')
arch=(any)
url="https://github.com/pydantic/jiter"
license=(MIT)
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-maturin"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools-rust"
  "${MINGW_PACKAGE_PREFIX}-python-pkgconfig"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
checkdepends=(
  # "${MINGW_PACKAGE_PREFIX}-python-dirty-equals"
  "${MINGW_PACKAGE_PREFIX}-python-pytest"
)
source=("$_realname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('fd8659548511171c1f84fbbdcd30e8696a8e8c2f44283ff7024902e5100419d5')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  cd crates/jiter-python
  msg2 "$PATH"
  export RUSTUP_TOOLCHAIN=stable
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"/crates/jiter-python
  ${MINGW_PREFIX}/bin/pip install dirty-equals
  ${MINGW_PREFIX}/bin/python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(${MINGW_PREFIX}/bin/python -c "import site; print(site.getsitepackages()[0])")
  msg2 "${site_packages#C:}"
  PYTHONPATH="$PWD/tmp_install/${site_packages#C:}" pytest
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="$pkgdir" crates/jiter-python/dist/*.whl
  install -vDm644 -t "$pkgdir${MINGW_PREFIX}/share/licenses/python-${_realname}" LICENSE
}

